
//Rules to build a tree object for a specific section
define_section.50: "[" DEFINE_SECTION_NAME "]" define_entry*
buildoption_section.40: "[" BUILD_OPTIONS_SECTION_NAME "]" buildoption_entry*
common_section.0: "[" SECTION_NAME "]" common_entry*

//Rules to handle some macros
include_statement: "!include" PATH

//Rules for build an entry for a section
define_entry: _NL STRING "=" STRING ("|" STRING)*
common_entry: _NL STRING ("|" STRING)*
buildoption_entry: _NL (STRING ":")? STRING "=" FLAG?

//More Terminals, specifically for section names
BUILD_OPTIONS_SECTION_NAME: "BuildOptions" SECTION_NAME?
DEFINE_SECTION_NAME: "Defines" SECTION_NAME?
SECTION_NAME: (LCASE_LETTER|UCASE_LETTER|DIGIT|" "|","|"."|"_"|"\"")+

//Terminals for matching. Needs to be manipulated by the Transformer
PATH: (LCASE_LETTER|UCASE_LETTER|DIGIT|VARIABLE|"/"|"\\"|".")+
STRING: CHARS+
FLAG: (CHARS|"="|":")+

// General use characters to create terminals
LCASE_LETTER: "a".."z"
UCASE_LETTER: "A".."Z"
DIGIT: "0".."9"
VARIABLE: ("$"|"("|")")
SYMBOLS: "_"|"."|"-"|"/"|"\\"|"\""|"$"|"("|")"|","|"*"|" "
CHARS: LCASE_LETTER|UCASE_LETTER|DIGIT|SYMBOLS

// New line characters
CR : /\r/
LF : /\n/
_NL.10: (CR? LF)+